import "task.mh";
import "../mm/pmm.mh";
import "../mm/mm.mh";
import "cast.mh";
import "../lib/linkedList.mh";
import "../libc/std.mh";

var GlobID: u64 = 0;
var TaskList: *LinkedList;
var Iterator: *LinkedItem;
var CurrentTask: *Task;

fn JmpTo(var Addr: *u64): void;

fn TaskWrapper(var Func: *u64): void {
  JmpTo(Func);
  while (1) {
  }
}

fn SchedSpawn(var Func: *u64): void {
  var Task: *Task = <*Task>HHalf(PmAlloc());
  Task.ID = GlobID;
  GlobID += 1;
  Task.PM = VmNewPM();
  Task.Ctx.RIP = <u64>TaskWrapper;
  Task.Ctx.RDI = <u64>Func;
  Task.Ctx.RSP = <u64>HHalf(<u64>PmAlloc() + 4096);
  Task.Ctx.CS = 0x28;
  Task.Ctx.SS = 0x30;
  Task.Ctx.RFlags = 0x202;
  LlNewItem(TaskList, Task);
  Iterator = TaskList.head.next;
}

fn Sched(var Ctx: *Context): void {
  if (CurrentTask != 0) {
    memcpy(&CurrentTask.Ctx, Ctx, 176);
  }
  Iterator = Iterator.next;
  if (Iterator == TaskList.head) {
    Iterator = TaskList.head.next;
  }
  CurrentTask = <*Task>Iterator.data;
  memcpy(Ctx, &CurrentTask.Ctx, 176);
  VmSwitchPM(CurrentTask.PM);
}

fn SchedRegister(): void {
  IrqInstall(0, Sched);
  CurrentTask = <*Task>0;
}

fn SchedInit(): void {
  TaskList = LlCreate();
}